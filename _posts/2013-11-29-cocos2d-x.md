---
layout: post
title: "Cocos2d-x で自前のシェーダを使ってみる"
date: 2013-11-29 00:00:00
categories: cocos2d-x shader

---

### 概要：

Cocos2d-x で自前のシェーダを使って、スプライトの色をセピア調に変更してみた。  
シェーダに uniform 値を渡すところで少し（時間的にはかなり）ハマった。  
セピア調に変更するコードは wgld.orgの『[セピア調変換](http://wgld.org/d/webgl/w054.html)』 を参考にさせて頂いた。

### 動作環境：

cocos2d-x-2.2.0

### 手順概要：

① 頂点シェーダ（sepia.vsh）と、フラグメントシェーダ（sepia.fsh）を用意する  
② CCGLProgram オブジェクトを生成して、CCSprite に適用する

### 手順詳細：

#### ① まず、頂点シェーダ（sepia.vsh）と、フラグメントシェーダ（sepia.fsh）を記述したファイルを用意する。

**sepia.vsh：**

```c
attribute vec4 a_position;
attribute vec2 a_texCoord;
attribute vec4 a_color;

#ifdef GL_ES
varying lowp vec4 v_fragmentColor;
varying mediump vec2 v_texCoord;
#else
varying vec4 v_fragmentColor;
varying vec2 v_texCoord;
#endif

void main() {
    gl_Position = CC_MVPMatrix * a_position;
    v_fragmentColor = a_color;
    v_texCoord = a_texCoord;
}
```

※ cocos2d-x 2.1.2 以降では CC_MVPMatrix を使う。   
公式フォーラムの『[problem with custom shader with cocos2d-x 2.1](http://www.cocos2d-x.org/forums/6/topics/19548)』を参照。

**sepia.fsh：**

```c
#ifdef GL_ES
precision lowp float;
#endif

// Input
varying vec2        v_texCoord;
uniform sampler2D   u_texture;
uniform bool        u_enableGray;
uniform bool        u_enableSepia;

// Gray
const float gRed    = 0.298912;
const float gGreen  = 0.586611;
const float gBlue   = 0.114478;
const vec3  gScale  = vec3(gRed, gGreen, gBlue);

// Sepia
const float sRed    = 1.07;
const float sGreen  = 0.74;
const float sBlue   = 0.43;
const vec3  sScale  = vec3(sRed, sGreen, sBlue);

void main()
{
    vec4  baseColor = texture2D(u_texture, v_texCoord);
    float grayColor = dot(baseColor.rgb, gScale);
    
    // Changes colors.
    if (u_enableSepia) {
        vec3 sepiaColor = vec3(grayColor) * sScale;
        baseColor = vec4(sepiaColor, 1.0);
    } else if (u_enableGray) {
        baseColor = vec4(vec3(grayColor), 1.0);
    }
    
    gl_FragColor = baseColor;
}
```

用意したファイルは、他のリソース（画像など）と同じようにプロジェクトに配置する。以下の例では APP_ROOT/Resources/shaders に配置した。

#### ② CCGLProgram オブジェクトを生成して、CCSprite に適用する。

**hoge.cpp（部分抜粋）：**

```cpp
// ...

// Create sprite as
// CCSprite *mySprite = ...

CCGLProgram *shader = new CCGLProgram();
mySprite->setShaderProgram(shader);

shader->initWithVertexShaderFilename("shaders/sepia.vsh", "shaders/sepia.fsh");
shader->addAttribute(kCCAttributeNamePosition, kCCVertexAttrib_Position);
shader->addAttribute(kCCAttributeNameTexCoord, kCCVertexAttrib_TexCoords);
shader->link();
shader->updateUniforms();

shader->setUniformLocationWith1i(shader->getUniformLocationForName("u_enableSepia"), true);
shader->setUniformLocationWith1i(shader->getUniformLocationForName("u_enableGray"), true);

// ...
```

uniform 値を渡すのは、`CCGLProgram::link` と `CCGLProgram::updateUniforms` を呼んだ後にする。link する前だと `CCGLProgram::getUniformLocationForName` が失敗（-1 を返す）するし、updateUniforms する前だと値が反映されない。ここでかなりハマった。

この件とは直接関係は無かったけど、.fsh や .vsh に定義してある変数であっても、出力結果に関係ないとコンパイラに無視されるため、getUniformLocation が失敗するというミスがよくあるらしい。OpenGL公式Wiki『[GLSL : common mistakes](http://www.opengl.org/wiki/GLSL_:_common_mistakes#glGetUniformLocation_and_glGetActiveUniform)』を参照。

### 感想：

- u_MVPMatrix が CC_MVPMatrix に変更されているのに気付かなくてハマった。そして、uniform 値を渡すところでかなりハマった。
- 対象の要素だけでなく、子要素にも同じ効果を与えたい（cascadeしたい）ときはどうすればいいのか？
- [wgld.org](http://wgld.org/) と [OpenGL Wiki](http://www.opengl.org/wiki/Main_Page) がすばらしい！
